apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "ngo.teog.swift"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 76
        versionName "1.4"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        // Adds exported schema location as test app assets.
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
}

dependencies {
    //OpenCSV for reading/writing CSV-formatted data
    def opencsv_version = "4.2"
    implementation "com.opencsv:opencsv:$opencsv_version"

    //GSON for serializing/deserializing objects using JSON representation
    def gson_version = "2.8.5"
    implementation "com.google.code.gson:gson:$gson_version"

    //Room Persistence Library for easy access of SQLite database
    def room_version = "2.2.5"
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    androidTestImplementation "androidx.room:room-testing:$room_version"

    //Dagger 2 for dependency injection
    def dagger_version = "2.24"
    implementation "com.google.dagger:dagger:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"

    //Zebra Crossing (ZXing) barcode scanner library
    def zxing_version = "3.5.0"
    implementation "com.journeyapps:zxing-android-embedded:$zxing_version"

    //Google Volley for managing HTTPS requests
    def volley_version = "1.1.1"
    implementation "com.android.volley:volley:$volley_version"

    //Android WorkManager for asynchronous tasks
    def work_version = "2.4.0"
    implementation "androidx.work:work-runtime:$work_version"

    //PhotoView provides an intuitive photo view implementation (that supports zooming)
    def photo_view_version = "2.3.0"
    implementation "com.github.chrisbanes:PhotoView:$photo_view_version"

    //FancyShowCaseView for tutorial/highlighting features
    def fancy_showcase_view_version = "1.3.1"
    implementation "me.toptas.fancyshowcase:fancyshowcaseview:$fancy_showcase_view_version"

    //Some miscellaneous dependencies

    implementation "android.arch.lifecycle:extensions:1.1.1"
    implementation "android.arch.lifecycle:viewmodel:1.1.1"

    implementation "androidx.lifecycle:lifecycle-common-java8:2.2.0"

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.0'

    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
