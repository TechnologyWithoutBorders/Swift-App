apply plugin: 'com.android.application'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'

android {
    compileSdkVersion 31
    defaultConfig {
        applicationId "ngo.teog.swift"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 91
        versionName "1.10"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        release {
            //minify breaks GSON serialization!
            //minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        // Adds exported schema location as test app assets.
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
}

dependencies {
    //OpenCSV for reading/writing CSV-formatted data
    def opencsv_version = "5.4"
    implementation "com.opencsv:opencsv:$opencsv_version"

    //GSON for serializing/deserializing objects using JSON representation
    def gson_version = "2.8.7"
    implementation "com.google.code.gson:gson:$gson_version"

    //Room Persistence Library for easy access of SQLite database
    def room_version = "2.4.2"
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    androidTestImplementation "androidx.room:room-testing:$room_version"

    //Dagger Android for dependency injection
    def dagger_version = "2.41"
    implementation "com.google.dagger:dagger-android:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-android-processor:$dagger_version"

    //Zebra Crossing (ZXing) barcode scanner library
    def zxing_version = "4.3.0"
    implementation "com.journeyapps:zxing-android-embedded:$zxing_version"

    //Google Volley for managing HTTPS requests
    def volley_version = "1.2.1"
    implementation "com.android.volley:volley:$volley_version"

    //PhotoView provides an intuitive photo view implementation (that supports zooming)
    def photo_view_version = "2.3.0"
    implementation "com.github.chrisbanes:PhotoView:$photo_view_version"

    //FancyShowCaseView for tutorial/highlighting features
    def fancy_showcase_view_version = "1.3.5"
    implementation "com.github.faruktoptas:FancyShowCaseView:$fancy_showcase_view_version"

    //Some miscellaneous dependencies

    //TODO this seems to be not necessary anymore or is it just buggy??
    def lifecycle_version = "2.4.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"

    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.0'

    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'

    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"

    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
