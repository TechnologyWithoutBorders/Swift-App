apply plugin: 'com.android.application'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'

android {
    compileSdk 35
    defaultConfig {
        applicationId "ngo.teog.swift"
        minSdkVersion 26
        targetSdkVersion 35
        versionCode 124
        versionName "1.21.3"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        release {
            //minify breaks GSON serialization!
            //minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        // Adds exported schema location as test app assets.
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
    namespace 'ngo.teog.swift'
}

dependencies {
    //OpenCSV for reading/writing CSV-formatted data
    def opencsv_version = '5.12.0'
    implementation "com.opencsv:opencsv:$opencsv_version"

    //GSON for serializing/deserializing objects using JSON representation
    def gson_version = '2.13.2'
    implementation "com.google.code.gson:gson:$gson_version"

    //Room Persistence Library for easy access of SQLite database
    def room_version = '2.8.1'
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    androidTestImplementation "androidx.room:room-testing:$room_version"

    //Dagger Android for dependency injection
    def dagger_version = '2.57.2'
    implementation "com.google.dagger:dagger-android:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-android-processor:$dagger_version"

    //Zebra Crossing (ZXing) barcode scanner library
    def zxing_version = "4.3.0"
    implementation "com.journeyapps:zxing-android-embedded:$zxing_version"

    //Google Volley for managing HTTPS requests
    def volley_version = "1.2.1"
    implementation "com.android.volley:volley:$volley_version"

    //PhotoView provides an intuitive photo view implementation (that supports zooming)
    def photo_view_version = "2.3.0"
    implementation "com.github.chrisbanes:PhotoView:$photo_view_version"

    //Image compression library
    //Do not use version 3.x yet!
    def compressor_version = "2.1.1"
    //noinspection GradleDependency
    implementation "id.zelory:compressor:$compressor_version"

    //FancyShowCaseView for tutorial/highlighting features
    def fancy_showcase_view_version = "1.3.9"
    implementation "com.github.faruktoptas:FancyShowCaseView:$fancy_showcase_view_version"

    //In-App updates for immediate updates
    implementation 'com.google.android.play:app-update:2.1.0'

    //Some miscellaneous dependencies

    //TODO this seems to be not necessary anymore or is it just buggy??
    def lifecycle_version = "2.9.4"
    implementation "androidx.lifecycle:lifecycle-viewmodel:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata:$lifecycle_version"
    //Java8 seems to mean "Java 8 and higher" here
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"

    implementation 'com.google.android.gms:play-services-oss-licenses:17.3.0'

    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.android.material:material:1.13.0'

    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"

    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test:runner:1.7.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
}
