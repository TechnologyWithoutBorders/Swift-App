apply plugin: 'com.android.application'
apply plugin: 'com.google.android.gms.oss-licenses-plugin'

android {
    compileSdkVersion 33
    defaultConfig {
        applicationId "ngo.teog.swift"
        minSdkVersion 24
        targetSdkVersion 33
        versionCode 116
        versionName "1.17"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        release {
            //minify breaks GSON serialization!
            //minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        // Adds exported schema location as test app assets.
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
    namespace 'ngo.teog.swift'
}

dependencies {
    // These constraints prevent a build error. Test if this is still necessary in the future!
    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.10") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.10") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }

    //OpenCSV for reading/writing CSV-formatted data
    def opencsv_version = "5.7.1"
    implementation "com.opencsv:opencsv:$opencsv_version"

    //GSON for serializing/deserializing objects using JSON representation
    def gson_version = "2.10.1"
    implementation "com.google.code.gson:gson:$gson_version"

    //Room Persistence Library for easy access of SQLite database
    def room_version = "2.5.1"
    implementation "androidx.room:room-runtime:$room_version"
    annotationProcessor "androidx.room:room-compiler:$room_version"
    androidTestImplementation "androidx.room:room-testing:$room_version"

    //Dagger Android for dependency injection
    def dagger_version = "2.45"
    implementation "com.google.dagger:dagger-android:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-android-processor:$dagger_version"

    //Zebra Crossing (ZXing) barcode scanner library
    def zxing_version = "4.3.0"
    implementation "com.journeyapps:zxing-android-embedded:$zxing_version"

    //Google Volley for managing HTTPS requests
    def volley_version = "1.2.1"
    implementation "com.android.volley:volley:$volley_version"

    //PhotoView provides an intuitive photo view implementation (that supports zooming)
    def photo_view_version = "2.3.0"
    implementation "com.github.chrisbanes:PhotoView:$photo_view_version"

    //Image compression library
    //Do not use version 3.x yet!
    def compressor_version = "2.1.1"
    //noinspection GradleDependency
    implementation "id.zelory:compressor:$compressor_version"

    //FancyShowCaseView for tutorial/highlighting features
    def fancy_showcase_view_version = "1.3.9"
    implementation "com.github.faruktoptas:FancyShowCaseView:$fancy_showcase_view_version"

    //In-App updates for immediate updates
    implementation 'com.google.android.play:app-update:2.1.0'

    //Some miscellaneous dependencies

    //TODO this seems to be not necessary anymore or is it just buggy??
    def lifecycle_version = "2.6.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"

    implementation 'com.google.android.gms:play-services-oss-licenses:17.0.1'

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'

    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"

    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}
